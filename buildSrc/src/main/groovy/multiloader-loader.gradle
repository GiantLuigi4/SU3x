plugins {
    id 'multiloader-common'
}

configurations {
    commonJava{
        canBeResolved = true
    }
    commonResources{
        canBeResolved = true
    }
}

String relPath(String tgt) {
    String pth = project.path
    pth = pth.substring(0, pth.lastIndexOf("_"))
    if (pth.equals(":")) pth = ""
    tgt = tgt.replace(":", "_")
//    println(pth + tgt)
    return pth + tgt
}

dependencies {
    String pth = relPath(':common')
    compileOnly(project(pth)) {
        capabilities {
//            requireCapability "$group:$mod_id"
        }
    }
    commonJava project(path: pth, configuration: 'commonJava')
    commonResources project(path: pth, configuration: 'commonResources')
}

tasks.named('compileJava', JavaCompile) {
    dependsOn(configurations.commonJava)
    source(configurations.commonJava)
}

processResources {
    dependsOn(configurations.commonResources)
    from(configurations.commonResources)
}

tasks.named('javadoc', Javadoc).configure {
    dependsOn(configurations.commonJava)
    source(configurations.commonJava)
}

tasks.named('sourcesJar', Jar) {
    dependsOn(configurations.commonJava)
    from(configurations.commonJava)
    dependsOn(configurations.commonResources)
    from(configurations.commonResources)
}
